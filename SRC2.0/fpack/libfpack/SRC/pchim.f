      SUBROUTINE PCHIM(N,X,F,D,INCFD,IERR)
C***BEGIN PROLOGUE  PCHIM
C***DATE WRITTEN   811103   (YYMMDD)
C***REVISION DATE  811104   (YYMMDD)
C***CATEGORY NO.  E1A
C***KEYWORDS  PIECEWISE CUBIC INTERPOLATION, MONOTONE INTERPOLATION,
C             CUBIC HERMITE INTERPOLATION
C***AUTHOR  FRITSCH,F.N.,LLNL
C             MATHEMATICS AND STATISTICS DIVISION
C             LAWRENCE LIVERMORE NATIONAL LABORATORY
C             P.O. BOX 808  (L-310)
C             LIVERMORE, CA  94550
C             FTS 532-4275
C***PURPOSE  SETS DERIVATIVES NEEDED TO DETERMINE A MONOTONE PIECEWISE
C            CUBIC HERMITE INTERPOLANT TO GIVEN DATA.  BOUNDARY VALUES
C            ARE PROVIDED WHICH ARE COMPATIBLE WITH MONOTONICITY.  THE
C            INTERPOLANT WILL HAVE AN EXTREMUM AT EACH POINT WHERE MONO-
C            TONICITY SWITCHES DIRECTION.  (SEE PCHIC IF USER CONTROL IS
C            DESIRED OVER BOUNDARY OR SWITCH CONDITIONS.)
C***DESCRIPTION
C
C          PCHIM:  PIECEWISE CUBIC HERMITE INTERPOLATION TO
C                  MONOTONE DATA.
C
C
C     TO FACILITATE TWO-DIMENSIONAL APPLICATIONS, INCLUDES AN INCREMENT
C     BETWEEN SUCCESSIVE VALUES OF THE F- AND D-ARRAYS.
C
C     THE RESULTING PIECEWISE CUBIC HERMITE FUNCTION MAY BE EVALUATED
C     BY PCHFE OR PCHFD.
C
C-----------------------------------------------------------------------
C
C  CALLING SEQUENCE:
C
C           CALL  PCHIM (N, X, F, D, INCFD, IERR)
C
      INTEGER  N, INCFD, IERR
      REAL  X(N), F(INCFD,N), D(INCFD,N)
C
C   PARAMETERS:
C
C     N -- (INPUT) NUMBER OF DATA POINTS.  (ERROR RETURN IF N.LT.2 .)
C           IF N=2, SIMPLY DOES LINEAR INTERPOLATION.
C
C     X -- (INPUT) REAL ARRAY OF INDEPENDENT VARIABLE VALUES.  THE
C           ELEMENTS OF X MUST BE STRICTLY INCREASING:
C                X(I-1) .LT. X(I),  I = 2(1)N.
C           (ERROR RETURN IF NOT.)
C
C     F -- (INPUT) REAL ARRAY OF DEPENDENT VARIABLE VALUES TO BE INTER-
C           POLATED.  F(1+(I-1)*INCFD) IS VALUE CORRESPONDING TO X(I).
C           PCHIM IS DESIGNED FOR MONOTONIC DATA, BUT IT WILL WORK FOR
C           ANY F-ARRAY.  IT WILL FORCE EXTREMA AT POINTS WHERE MONO-
C           TONICITY SWITCHES DIRECTION.  IF SOME OTHER TREATMENT OF
C           SWITCH POINTS IS DESIRED, PCHIC SHOULD BE USED INSTEAD.
C                                     -----
C
C     D -- (OUTPUT) REAL ARRAY OF DERIVATIVE VALUES AT THE DATA POINTS.
C           IF THE DATA ARE MONOTONIC, THESE VALUES WILL DETERMINE A
C           A MONOTONE CUBIC HERMITE FUNCTION.
C           THE VALUE CORRESPONDING TO X(I) IS STORED IN
C                D(1+(I-1)*INCFD),  I=1(1)N.
C           NO OTHER ENTRIES IN D ARE CHANGED.
C
C     INCFD -- (INPUT) INCREMENT BETWEEN SUCCESSIVE VALUES IN F AND D.
C           THIS ARGUMENT IS PROVIDED PRIMARILY FOR 2-D APPLICATIONS.
C           (ERROR RETURN IF  INCFD.LT.1 .)
C
C     IERR -- (OUTPUT) ERROR FLAG.
C           NORMAL RETURN:
C              IERR = 0  (NO ERRORS).
C           WARNING ERROR:
C              IERR.GT.0  MEANS THAT IERR SWITCHES IN THE DIRECTION
C                 OF MONOTONICITY WERE DETECTED.
C           "RECOVERABLE" ERRORS:
C              IERR = -1  IF N.LT.2 .
C              IERR = -2  IF INCFD.LT.1 .
C              IERR = -3  IF THE X-ARRAY IS NOT STRICTLY INCREASING.
C             (THE D-ARRAY HAS NOT BEEN CHANGED IN ANY OF THESE CASES.)
C               NOTE:  THE ABOVE ERRORS ARE CHECKED IN THE ORDER LISTED,
C                   AND FOLLOWING ARGUMENTS HAVE **NOT** BEEN VALIDATED.
C
C-----------------------------------------------------------------------
C
C***REFERENCES  1. F.N.FRITSCH AND R.E.CARLSON, "MONOTONE PIECEWISE
C                 CUBIC INTERPOLATION," SIAM J.NUMER.ANAL. 17, 2 (APRIL
C                 1980), 238-246.
C               2. F.N.FRITSCH AND J.BUTLAND, "AN IMPROVED MONOTONE
C                 PIECEWISE CUBIC INTERPOLATION ALGORITHM," LLNL REPORT
C                 UCRL-85104 (OCTOBER 1980).
C***ROUTINES CALLED  (NONE)
C***END PROLOGUE  PCHIM
C-----------------------------------------------------------------------
C
C  PROGRAMMING NOTES.
C
C     1. TO PRODUCE A DOUBLE PRECISION VERSION, SIMPLY:
C        A. CHANGE PCHIM TO DPCHIM WHEREVER IT OCCURS,
C        B. CHANGE THE REAL DECLARATIONS TO DOUBLE PRECISION, AND
C        C. CHANGE THE CONSTANT ZERO TO DOUBLE PRECISION.
C
C-----------------------------------------------------------------------
C
C  LOCAL DECLARATIONS.
C
      INTEGER  I, NLESS1
      REAL  DEL1, DEL2, DPROD, DSAVE, H1, H2, HSUM, HSUMT3, W1, W2, ZERO
      DATA  ZERO /0./
C
C  VALIDITY-CHECK ARGUMENTS.
C
C***FIRST EXECUTABLE STATEMENT  PCHIM
      IF ( N.LT.2 )  GO TO 5001
      IF ( INCFD.LT.1 )  GO TO 5002
      DO 1  I = 2, N
         IF ( X(I).LE.X(I-1) )  GO TO 5003
    1 CONTINUE
C
C  FUNCTION DEFINITION IS OK, GO ON.
C
      IERR = 0
      NLESS1 = N - 1
      H1 = X(2) - X(1)
      DEL1 = (F(1,2) - F(1,1))/H1
      DSAVE = DEL1
C
C  SPECIAL CASE N=2 -- USE LINEAR INTERPOLATION.
C
      IF (NLESS1 .GT. 1)  GO TO 10
      D(1,1) = DEL1
      D(1,N) = DEL1
      GO TO 5000
C
C  NORMAL CASE  (N .GE. 3).
C
   10 CONTINUE
      H2 = X(3) - X(2)
      DEL2 = (F(1,3) - F(1,2))/H2
C
C  SET D(1) VIA NON-CENTERED THREE-POINT FORMULA.
C
      D(1,1) = ((H1+H1+H2)*DEL1 - H1*DEL2)/(H1+H2)
      IF (D(1,1)*DEL1 .LE. ZERO)  D(1,1) = ZERO
C
C  LOOP THROUGH INTERIOR POINTS.
C
      DO 50  I = 2, NLESS1
         IF (I .EQ. 2)  GO TO 40
C
         H1 = H2
         H2 = X(I+1) - X(I)
         DEL1 = DEL2
         DEL2 = (F(1,I+1) - F(1,I))/H2
   40    CONTINUE
C
C        SET D(I)=0 UNLESS DATA ARE STRICTLY MONOTONIC.
C
         D(1,I) = ZERO
         DPROD = DEL1*DEL2
         IF (DPROD .GT. ZERO)  GO TO 45
C
C        COUNT NUMBER OF CHANGES IN DIRECTION OF MONOTONICITY.
C
         IF (DPROD .LT. ZERO)  GO TO 42
C
         IF (DEL2 .EQ. ZERO)  GO TO 50
         IF (DSAVE*DEL2 .LT. ZERO)  IERR = IERR + 1
         DSAVE = DEL2
         GO TO 50
C
   42    CONTINUE
         IERR = IERR + 1
         DSAVE = DEL2
         GO TO 50
C
C        USE BRODLIE MODIFICATION OF BUTLAND FORMULA.
C
   45    CONTINUE
         HSUM = H1 + H2
         HSUMT3 = HSUM+HSUM+HSUM
         W1 = (HSUM + H2)/HSUMT3
         W2 = (HSUM + H1)/HSUMT3
         D(1,I) = DPROD/(W1*DEL2 + W2*DEL1)
C
   50 CONTINUE
C
C  SET D(N) VIA NON-CENTERED THREE-POINT FORMULA.
C
      D(1,N) = ((H2+H2+H1)*DEL2 - H2*DEL1)/(H1+H2)
      IF (D(1,N)*DEL2 .LE. ZERO)  D(1,N) = ZERO
C
C  NORMAL RETURN.
C
 5000 CONTINUE
      RETURN
C
C  ERROR RETURNS.
C
 5001 CONTINUE
C     N.LT.2 RETURN.
      IERR = -1
      CALL XERROR ('PCHIM -- NUMBER OF DATA POINTS LESS THAN TWO',
     *  44, IERR, 1)
      RETURN
C
 5002 CONTINUE
C     INCFD.LT.1 RETURN.
      IERR = -2
      CALL XERROR ('PCHIM -- INCREMENT LESS THAN ONE',
     *  32, IERR, 1)
      RETURN
C
 5003 CONTINUE
C     X-ARRAY NOT STRICTLY INCREASING.
      IERR = -3
      CALL XERROR ('PCHIM -- X-ARRAY NOT STRICTLY INCREASING',
     *  40, IERR, 1)
      RETURN
C------------- LAST LINE OF PCHIM FOLLOWS ------------------------------
      END
