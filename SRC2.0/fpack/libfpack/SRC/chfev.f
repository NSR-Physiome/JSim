      SUBROUTINE CHFEV(X1,X2,F1,F2,D1,D2,NE,XE,FE,NEXT,IERR)
C***BEGIN PROLOGUE  CHFEV
C***DATE WRITTEN   811019   (YYMMDD)
C***REVISION DATE  811028   (YYMMDD)
C***CATEGORY NO.  E1A
C***KEYWORDS  CUBIC POLYNOMIAL EVALUATION, CUBIC HERMITE EVALUATION
C***AUTHOR  FRITSCH,F.N.,LLNL
C             MATHEMATICS AND STATISTICS DIVISION
C             LAWRENCE LIVERMORE NATIONAL LABORATORY
C             P.O. BOX 808  (L-310)
C             LIVERMORE, CA  94550
C             FTS 532-4275
C***PURPOSE  EVALUATES A CUBIC POLYNOMIAL GIVEN IN HERMITE FORM AT AN
C            ARRAY OF POINTS.  WHILE DESIGNED FOR USE BY PCHFE, IT MAY
C            BE USEFUL DIRECTLY AS AN EVALUATOR FOR A PIECEWISE CUBIC
C            HERMITE FUNCTION IN APPLICATIONS, SUCH AS GRAPHING, WHERE
C            THE INTERVAL IS KNOWN IN ADVANCE.
C***DESCRIPTION
C
C  CALLING SEQUENCE:
C
C           CALL  CHFEV (X1, X2, F1, F2, D1, D2, NE, XE, FE, NEXT, IERR)
C
      INTEGER  NE, NEXT(2), IERR
      REAL  X1, X2, F1, F2, D1, D2, XE(NE), FE(NE)
C
C   PARAMETERS:
C
C     X1,X2 -- (INPUT) ENDPOINTS OF INTERVAL OF DEFINITION OF CUBIC.
C           (ERROR RETURN IF  X1.EQ.X2 .)
C
C     F1,F2 -- (INPUT) VALUES OF FUNCTION AT X1 AND X2, RESPECTIVELY.
C
C     D1,D2 -- (INPUT) VALUES OF DERIVATIVE AT X1 AND X2, RESPECTIVELY.
C
C     NE -- (INPUT) NUMBER OF EVALUATION POINTS.  (ERROR RETURN IF
C           NE.LT.1 .)
C
C     XE -- (INPUT) REAL ARRAY OF POINTS AT WHICH THE FUNCTIONS ARE TO
C           BE EVALUATED.  IF ANY OF THE XE ARE OUTSIDE THE INTERVAL
C           [X1,X2], A WARNING ERROR IS RETURNED.
C
C     FE -- (OUTPUT) REAL ARRAY OF VALUES OF THE CUBIC FUNCTION DEFINED
C           BY  X1,X2, F1,F2, D1,D2  AT THE POINTS  XE.
C
C     NEXT -- (OUTPUT) INTEGER ARRAY INDICATING NUMBER OF EXTRAPOLATION
C           POINTS:
C            NEXT(1) = NUMBER OF EVALUATION POINTS TO LEFT OF INTERVAL.
C            NEXT(2) = NUMBER OF EVALUATION POINTS TO RIGHT OF INTERVAL.
C
C     IERR -- (OUTPUT) ERROR FLAG.
C           NORMAL RETURN:
C              IERR = 0  (NO ERRORS).
C           "RECOVERABLE" ERRORS:
C              IERR = -1  IF NE.LT.1 .
C              IERR = -2  IF X1.EQ.X2 .
C                (OUTPUT ARRAYS HAVE NOT BEEN CHANGED IN EITHER CASE.)
C
C-----------------------------------------------------------------------
C***REFERENCES  (NONE)
C***ROUTINES CALLED  (NONE)
C***END PROLOGUE  CHFEV
C-----------------------------------------------------------------------
C
C  PROGRAMMING NOTES.
C
C     1. TO PRODUCE A DOUBLE PRECISION VERSION, SIMPLY:
C        A. CHANGE CHFEV TO DCHFEV WHEREVER IT OCCURS,
C        B. CHANGE THE REAL DECLARATION TO DOUBLE PRECISION,
C        C. CHANGE THE CONSTANT ZERO TO DOUBLE PRECISION, AND
C        D. CHANGE THE NAMES OF THE FORTRAN FUNCTIONS:  AMAX1, AMIN1.
C
C-----------------------------------------------------------------------
C
C  LOCAL DECLARATIONS.
C
      INTEGER  I
      REAL  C2, C3, DEL1, DEL2, DELTA, H, X, XMI, XMA, ZERO
      DATA  ZERO /0./
C
C  VALIDITY CHECKS FOR INPUTS.
C
C***FIRST EXECUTABLE STATEMENT  CHFEV
      IF (NE .LT. 1)  GO TO 5001
      H = X2 - X1
      IF (H .EQ. ZERO)  GO TO 5002
C
C  INITIALIZE.
C
      IERR = 0
      NEXT(1) = 0
      NEXT(2) = 0
      XMI = AMIN1(ZERO, H)
      XMA = AMAX1(ZERO, H)
C
C  COMPUTE CUBIC COEFFICIENTS (EXPANDED ABOUT X1).
C
      DELTA = (F2 - F1)/H
      DEL1 = (D1 - DELTA)/H
      DEL2 = (D2 - DELTA)/H
C                                           (DELTA IS NO LONGER NEEDED.)
      C2 = -(DEL1+DEL1 + DEL2)
      C3 = (DEL1 + DEL2)/H
C                               (H, DEL1 AND DEL2 ARE NO LONGER NEEDED.)
C
C  EVALUATION LOOP.
C
      DO 500  I = 1, NE
         X = XE(I) - X1
         FE(I) = F1 + X*(D1 + X*(C2 + X*C3))
C          COUNT EXTRAPOLATION POINTS.
         IF ( X.LT.XMI )  NEXT(1) = NEXT(1) + 1
         IF ( X.GT.XMA )  NEXT(2) = NEXT(2) + 1
C        (NOTE REDUNDANCY: IF EITHER CONDITION IS TRUE, OTHER IS FALSE.)
  500 CONTINUE
C
C  NORMAL RETURN.
C
      RETURN
C
C  ERROR RETURNS.
C
 5001 CONTINUE
C     NE.LT.1 RETURN.
      IERR = -1
      CALL XERROR ('CHFEV -- NUMBER OF EVALUATION POINTS LESS THAN ONE',
     *  50, IERR, 1)
      RETURN
C
 5002 CONTINUE
C     X1.EQ.X2 RETURN.
      IERR = -2
      CALL XERROR ('CHFEV -- INTERVAL ENDPOINTS EQUAL', 33, IERR, 1)
      RETURN
C------------- LAST LINE OF CHFEV FOLLOWS ------------------------------
      END
